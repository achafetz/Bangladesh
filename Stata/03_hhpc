/*-------------------------------------------------------------------------------
# Name:		03_hhpc
# Purpose:	Process household data and create physical capital indices
# Author:	Tim Essam, Ph.D.
# Created:	2014/11/05
# Modified: 2014/11/05
# Owner:	USAID GeoCenter | OakStream Systems, LLC
# License:	MIT License
# Ado(s):	labutil, labutil2 (ssc install labutil, labutil2)
# Dependencies: copylables, attachlabels, 00_SetupFoldersGlobals.do
#-------------------------------------------------------------------------------
*/
clear
capture log close
log using "$pathlog/hhpc", replace
set more off

/* Load module with information on household assets. */
use "$pathin/006_mod_d1_male.dta", clear

* Create variables reflecting ownership of different durable assets
/* Automate the process a bit to make processing easier*/

* create vector of assets for which binary variables are created
#delimit ;
local assets trunk bucket stove cookpots bed cabinet 
	table hukka fan iron radio cd clock tvbw tvclr
	gold sew bike rickshaw van boat elecboat moto 
	mobile landphone thresher mill randa saw hammer patkoa
	fishnet spade axe shovel shabol daa horse mule
	donkey;
#delimit cr
local count = 1
foreach x of local assets {
	bys a01: g byte `x' = d1_02 == `count' & d1_03 == 1
	la var `x' "HH owns `x'"
	
	* Check that asset matches order
	display in yellow "`x': `count' asset code"
	local count = `count'+1
	}
*end

by a01: g cashOnHand = d1_04 if d1_02 == 42 
replace cashOnHand = 0 if cashOnHand == .
la var cashOnHand "Cash on hand at time of survey"

by a01: g jewelryVal = d1_10 if d1_02 == 16
replace jewelryVal = 0 if jewelryVal == .
la var jewelryVal "Value of jewelry"

by a01: g byte solar = d1_02 == 43 & d1_03 == 1
la var solar "hh has solar energy panel"

by a01: g byte generator = d1_02 == 44 & d1_03 == 1
la var generator "hh has electricity generator"

* Exchange rate was about 0.014 dollars to Taka in 2011
* or about 71 Taka to dollars 
* Calculate value of durables owned and check distribution
sort a01 d1_02
egen hhDurablesValue = sum(d1_10), by(a01 d1_02)
tabstat hhDurablesValue, by(d1_02) stat(mean sd min max)

* Collapse down to HH level
include "$pathdo/copylabels.do"
collapse (sum) hhDurablesValue (max) trunk-generator, by(a01)
include "$pathdo/attachlabels.do"

* Run summary statistics on assets
tabstat trunk-generator, stat(mean sd min max)

* Create wealth index based on durable assets
#delimit ;
factor trunk bucket stove cookpots bed cabinet table hukka fan 
	iron radio cd clock tvbw tvclr sew bike rickshaw van boat 
	elecboat moto mobile landphone, pcf;
#delimit cr
rotate
predict wealth

* Plot loadings for review
loadingplot, mlabs(small) mlabc(maroon) mc(maroon) /*
	*/ xline(0, lwidth(med) lpattern(tight_dot) lcolor(gs10)) /*
	*/ yline(0, lwidth(med) lpattern(tight_dot) lcolor(gs10)) /*
	*/ title(Household wealth index loadings)
graph export "$pathgraph/wealthLoadings.png", as(png) replace
scree, title(Scree plot of wealth index)
graph export "$pathgragh/wealthScree.png", as(png) replace

* Generate variable reflecting fungible wealth
g fungWealth = jewelryVal + cashOnHand
la var fungWealth "fungible wealth holdings"

save "$pathout/hhdurables.dta", replace

*************
* Ag Assets *
*************
bob

*TODO: fix inlist commands, change look-up codes, translate items in workbook.

*load module
use "$pathin/007_mod_d2_male.dta", clear
g handtool= as06q04 if inlist(as06q02, 1, 2, 3, 4, 5, 6, 7)==1 & as06q03==1
g plow = 	as06q04 if inlist(as06q02, 8)==1 & as06q03==1
g cart = 	as06q04 if inlist(as06q02, 9)==1 & as06q03==1
g tractor = as06q04 if inlist(as06q02, 10)==1 & as06q03==1
g yoke = 	as06q04 if inlist(as06q02, 11)==1 & as06q03==1
g seeder = 	as06q04 if inlist(as06q02, 12)==1 & as06q03==1
g sprayer = as06q04 if inlist(as06q02, 13)==1 & as06q03==1
g watering = as06q04 if inlist(as06q02, 15)==1 & as06q03==1
g motorpump = as06q04 if inlist(as06q02, 16)==1 & as06q03==1
g thresher = as06q04 if inlist(as06q02, 17)==1 & as06q03==1
g storage = as06q04 if inlist(as06q02, 18)==1 & as06q03==1
g chxpen = as06q04 if inlist(as06q02, 19)==1 & as06q03==1
g shed = 	as06q04 if inlist(as06q02, 20)==1 & as06q03==1
g generator = as06q04 if inlist(as06q02, 21)==1 & as06q03==1
g dryshed = as06q04 if inlist(as06q02, 22)==1 & as06q03==1
g sheller = as06q04 if inlist(as06q02, 23)==1 & as06q03==1
g baler = 	as06q04 if inlist(as06q02, 24)==1 & as06q03==1
g silo = 	as06q04 if inlist(as06q02, 25)==1 & as06q03==1

*Apply lables to variables
foreach name of varlist handtool-silo {
	la var `name' "No. `name's owned by hh"
	by hid: egen n`name' = total(`name')
	la var n`name' "Total `name's owned by hh"
}
*end













* Create TLU (based on values from http://www.fao.org/wairdocs/ilri/x5443e/x5443e04.htm)
/* Notes: Sheep includes sheep and goats
Horse includes all draught animals (donkey, horse, bullock)
chxTLU includes all small animals (chicken, fowl, etc).
*/

g camelVal = 1.0
g cattleVal = 0.70
g sheepVal = 0.10
g horsesVal = 0.80
g mulesVal = 0.70
g assesVal = 0.50
g chxVal = 0.01



